
##############################################################################################

#This Snakefile takes the sequences of VAMB clusters and produces a gene count matrix of the nonredundant genes for each cluster (also excluding representative sequences)

#input files is found in
#/home/projects/cu_10108/people/trizac/VAMB_genes/contigs

# The output is:
#    nonredundant_gene_clusters/gene_count_matrix.tsv: Gene count matrix of the readcounts of all genes of the unique genes
# 

##############################################################################################

#!python

#!/usr/bin/env python3
import os
import sys

#Get IDs
IDS, =  glob_wildcards("/home/projects/cu_10108/people/trizac/VAMB_genes/contigs/cluster_{id}.fna")
SAMPLES, = glob_wildcards("/home/projects/cu_10108/people/trizac/reads/{sample}_1.fastq")

#print(SAMPLES[1:10], len(SAMPLES))
#print(IDS[1:10], len(IDS))

#Helper rule
rule all:
        input: "prodigal_genes/mmseqs_95id/all_genes95_rep_seq.fasta",
               "nonredundant_gene_clusters/gene_count_matrix.tsv",
                expand("nonredundant_gene_clusters/nucleotides/{id}_nonredundant.fna", id=IDS)


# Use prodigal to predict genes
rule gene_predict:
    input:
        "contigs/cluster_{id}.fna"
    output:
        protein = "prodigal_genes/proteins/{id}.faa",
        nuceotides = "prodigal_genes/nucleotides/{id}.fna"
    threads: 1
    resources:
        mem_gb = 20,
        runtime = 43200
    shell:
        "prodigal -i {input} -a {output.protein} -d {output.nuceotides} -p meta"

# combine genes from all the samples
rule combine_genes:
    input:
        prot = expand("prodigal_genes/proteins/{id}.faa", id=IDS),
        nuc = expand("prodigal_genes/nucleotides/{id}.fna", id=IDS)
    output:
        prot = "prodigal_genes/all_genes.faa",
        nuc = "prodigal_genes/all_genes.fna"
    threads: 1
    resources:
        mem_gb = 60,
        runtime = 10000
    shell:
        "cat {input.prot} > {output.prot}; cat {input.nuc} > {output.nuc};" #for file in {input}; do gzip -f $file; done


# Get representative genes from all genes.
rule repres_genes:
    input:
        "prodigal_genes/all_genes.faa"
    output:
        "prodigal_genes/mmseqs_95id/all_genes95_rep_seq.fasta"
    threads: 14
    resources:
        mem_gb = 50,
        runtime = 86400
    shell:
        "mmseqs easy-linclust --min-seq-id 0.95 -c 0.95 --threads {threads} {input} prodigal_genes/mmseqs_95id/all_genes95 tmp;"


# Creating a file only containing the redundant genes and their representatives
# Based on the clustering file from mmseqs
rule redundant_genes:
    input:
        "prodigal_genes/mmseqs_95id/all_genes95_cluster.tsv"
    output:
        "prodigal_genes/mmseqs_95id/redundant_genes.tsv"
    threads: 1
    resources:
        mem_gb = 50,
        runtime = 86400
    shell: 
        "awk '$1!=$2' {input} | cut -f2 | uniq > {output}"


# Creating a fasta file for each cluster only containing non-redundant genes (also excluding representatives)
rule nonredundant_clusters:
    input: 
        prot = "prodigal_genes/proteins/{id}.faa",
        nuc = "prodigal_genes/nucleotides/{id}.fna",
        redundant = "prodigal_genes/mmseqs_95id/redundant_genes.tsv"
    output:
        prot_nonredundant = "nonredundant_gene_clusters/proteins/{id}_nonredundant.faa",
        nuc_nonredundant = "nonredundant_gene_clusters/nucleotides/{id}_nonredundant.fna"
    threads: 1
    resources:
        mem_gb = 50,
        runtime = 86400
    shell:
        "filterbyname.sh in={input.prot} out={output.prot_nonredundant} names={input.redundant} include=f; "
        "filterbyname.sh in={input.nuc} out={output.nuc_nonredundant} names={input.redundant} include=f"

rule nonredundant_catalogue:
    input:
        genecat = "prodigal_genes/all_genes.faa",
        redundant = "prodigal_genes/mmseqs_95id/redundant_genes.tsv"
    output:
        genecat_nonredundant = "nonredundant_gene_clusters/all_genes_nonredundant.fasta"
    threads: 1
    resources:
        mem_gb = 50,
        runtime = 86400
    shell:
        "filterbyname.sh in={input.genecat} out={output.genecat_nonredundant} names={input.redundant} include=f"

# The reads are mapped to the nonredundant gene catalogue with minimap2
rule minimap_index:
    input:
        "nonredundant_gene_clusters/all_genes_nonredundant.fasta"
    output:
        "nonredundant_gene_clusters/nonredundant.mmi"
    threads: 1
    resources:
        mem_gb = 50,
        runtime = 86400
    shell:
        "minimap2 -d {output} {input}"

rule minimap_align:
    input:
        index = "nonredundant_gene_clusters/nonredundant.mmi",
        gene_cat = "nonredundant_gene_clusters/all_genes_nonredundant.fasta",
        fastq1 = "/home/projects/cu_10108/people/trizac/reads/{sample}_1.fastq",
       	fastq2 = "/home/projects/cu_10108/people/trizac/reads/{sample}_2.fastq"
    output:
        "nonredundant_gene_clusters/mapped_reads/gene_counts_{sample}.bam"
    threads: 40
    resources:
        mem_gb = 188,
        runtime = 86400 #24h
    shell:
        "minimap2 -v 1 -t 40 -N 50 -ax sr {input.index} {input.fastq1} {input.fastq2} | samtools view -T {input.gene_cat} -F 3584 -b --threads {threads} | samtools sort --threads {threads} > {output}"


# Use samtools to count the genes
rule count_genes:
    input:
        "nonredundant_gene_clusters/mapped_reads/gene_counts_{sample}.bam"
    output:
        count = "nonredundant_gene_clusters/counts/{sample}.counts"
    threads: 1
    resources:
        mem_gb = 20,
        runtime = 1800 #30min
    shell:
        "samtools idxstats {input} | cut -f3 > {output.count}"

# Create the gene index
rule gene_names:
    input:
        "nonredundant_gene_clusters/mapped_reads/gene_counts_{sample}.bam"
    output:
        "nonredundant_gene_clusters/mapped_reads/gene_names_{sample}"
    wildcard_constraints:
        sample=SAMPLES[0]
    threads: 1
    resources:
        mem_gb = 20,
        runtime = 1800 #30min
    shell:
        "samtools idxstats {input} | cut -f1 > {output}"

# Create the header for the matrix
rule create_header:
    input: 
        expand("nonredundant_gene_clusters/counts/{sample}.counts", sample=SAMPLES)
    output:
        header = "nonredundant_gene_clusters/mapped_reads/header.txt"
    threads: 1
    resources:
        mem_gb = 188,
        runtime = 1800 #30min
    run:
        header = "Gene"
        for f in input:
            sample_name = f.split("/")[-1].split(".")[0]
            header = header + "\t" + sample_name
        with open(output.header, "w") as out:
            out.write(header)


#Combining the gene names with the counts of all samples
rule gene_count_matrix:
    input:
        counts = expand("nonredundant_gene_clusters/counts/{sample}.counts", sample=SAMPLES),
        gene_names = expand("nonredundant_gene_clusters/mapped_reads/gene_names_{sample}", sample=SAMPLES[0]),
        header = "nonredundant_gene_clusters/mapped_reads/header.txt"
    output:
        "nonredundant_gene_clusters/gene_count_matrix.tsv"
    threads: 1
    resources:
        mem_gb = 188,
        runtime = 1800 #30min
    shell:
        "paste {input.gene_names} {input.counts} | cat {input.header} - > {output}"
